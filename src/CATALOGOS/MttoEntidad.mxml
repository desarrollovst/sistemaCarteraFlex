<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="400" height="250" xmlns:Data="Data.*">
	
	<mx:Validator id="codigoV" source="{txtCodigo}" property="text" 
	invalid="{txtCodigo.setFocus()}" triggerEvent="" requiredFieldError="Código Requerido"/>
	
	<mx:Validator id="descripcionV" source="{txtDescripcion}" property="text" 
	invalid="{txtDescripcion.setFocus()}" triggerEvent="" requiredFieldError="Descripción Requerida"/>
	
	<mx:NumberValidator id="critV" source="{cboCritRiesgo}" property="selectedIndex" 
	invalid="{cboCritRiesgo}" minValue="1" triggerEvent="" lowerThanMinError="Criterio de Riesgo Requerido"/>
	
	<mx:Script>
		<![CDATA[
		import Data.DatosEntidad;
		import Data.EventEntidad;
		import Data.Globales;
		import Data.Utils;
		import mx.collections.ArrayCollection;
		import mx.controls.Alert;
		import mx.core.Application;
		import mx.effects.*;
		import mx.events.EffectEvent;
		import mx.managers.PopUpManager;	
		import mx.rpc.events.ResultEvent;
		import mx.rpc.soap.WebService;
		import mx.validators.Validator;	
		
		private var info:DatosEntidad;
		
		private var _xmlEnt:XML = new XML();
		private var _xmlCrit:XML = new XML();
		
		private var critObj:ArrayCollection = new ArrayCollection();
		
		public var global:Globales;
		public var openEffect:Effect = new WipeDown();
		public var closeEffect:Effect = new WipeDown();
		public var wsMS:WebService;	
		public var tipoAccion:int;
		public var titulo:String;
		public var du:Utils;
		
		public function cargaEntidad(codigo:String):void{
			var wsCat:WebService = new WebService;
			var params:Array = new Array;
			tipoAccion = 2; 
			this.title = "Edición " + titulo;
			info = new DatosEntidad();
			//Datos del Destino de los Recursos
			du.initWsCat(wsCat);
			du.sCursor();
	
			du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
				_xmlEnt = new XML(evt.result.toString()); 
			
				du.rCursor();
				du.closeWs(wsCat);
				
				if (_xmlEnt.elements().length() > 0){
					llenaRegistros();
				}
			});
			params[0] = codigo;
			//Obtiene detalle de informacion de la Entidad Federativa
			wsCat.getInfoRegistro.send(27, params);
		}
		
		public function cerrar():void{
			var playReverse:Boolean = true;   
            closeEffect.duration = 500;
            closeEffect.play([this], playReverse);
            closeEffect.addEventListener(EffectEvent.EFFECT_END, terminarEfecto);
		}
	
		public function enviar():void{
			var invalidArray:Array = Validator.validateAll([codigoV, descripcionV, critV]);
		
			if(invalidArray.length == 0){	
				info.codigo = txtCodigo.text;
				info.nombre = txtDescripcion.text;
				info.cdgCrit = cboCritRiesgo.selectedItem.id;
				info.guarda = true;
			}
			else
				info.guarda = false;
			validaFinal();
		}
		
		private function formateaCriterio():void{
			var i:int;
			var cont:int = _xmlCrit.elements().length();
			var oItem:Object;
			var item:Array = new Array();
			
			oItem = new Object();
			oItem.id = "0";
			oItem.descripcion = "--Seleccionar--";
			item.push(oItem);
			
			for (i = 0; i < cont; i++){
				oItem = new Object();
				oItem.id = _xmlCrit.Table[i].CODIGO.toString();
				oItem.descripcion = _xmlCrit.Table[i].DESCRIPCION.toString();
				item.push(oItem);
			}
			critObj = new ArrayCollection(item);
		}
		
		public function guardaEntidad():void{
			initConexion();
			du.sCursor();
			global.bloquear();
			wsMS.addEventListener(ResultEvent.RESULT, setAccionUbicacion);
			wsMS.setAccionUbicacion.send(tipoAccion, info.codigo, info.nombre, info.cdgCrit);	
		}
	
		public function init(tipo:int, codigo:String):void{
			var wsCat:WebService = new WebService;
			var params:Array = new Array;
			global = new Globales();
			du = new Utils(); 
			titulo = "Ubicación Geográfica";
			//Codigo que permite ejecutar el efecto de apertura
			openEffect.duration = 500;
			openEffect.play([this]);
			
			du.initWsCat(wsCat);
			du.sCursor();
	
			du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
				_xmlCrit = new XML(evt.result.toString()); 
			
				du.rCursor();
				du.closeWs(wsCat);
				
				if (_xmlCrit.elements().length() > 0){
					formateaCriterio();
					cboCritRiesgo.dataProvider = critObj;
				}
				if(tipo == 1)
					registraEntidad();
				else if(tipo == 2)
					cargaEntidad(codigo);
			});
			params[0] = "UG";
			wsCat.getListado.send(44, params);
		}
		
		public function initConexion():void{				
			wsMS = new WebService();			
			wsMS.wsdl = Application.application.wsStr.wsdlServ.toString();
			wsMS.loadWSDL();
		}	
		
		public function llenaRegistros():void{
			txtCodigo.setFocus();
			txtCodigo.editable = false;
			txtDescripcion.editable = false;
			
			txtCodigo.text = _xmlEnt.Table[0].CODIGO;
			txtDescripcion.text = _xmlEnt.Table[0].NOMBRE;
			
			for(var i:int = 0; i < critObj.length; i++){
				if(critObj[i].id == _xmlEnt.Table[0].CDGCRRI){
					cboCritRiesgo.selectedIndex = i;
					break;		
				}
			}
		}
			
		public function registraEntidad():void{
			tipoAccion = 1;
			this.title = "Nuevo " + titulo;
			info = new DatosEntidad();
			
			txtCodigo.setFocus();
			txtCodigo.editable = true;
		}
		
		private function setAccionUbicacion(event:ResultEvent):void{
			wsMS.disconnect();
			du.rCursor();
			global.desbloquear();
			wsMS.removeEventListener(ResultEvent.RESULT, setAccionUbicacion);
			var res:String = event.result.toString();
			if (res.substr(0,1) == "1"){
				cerrar();
			}	
			else
				Alert.show(res.substr(2,res.length -1),titulo,4,null,null,global.iAlert);		
		}
		
		public function terminarEfecto(event:EffectEvent):void{
			closeEffect.removeEventListener(EffectEvent.EFFECT_END, terminarEfecto);
			PopUpManager.removePopUp(this);
		}
		
		public function validaDatosEnt(event:EventEntidad):void{
			info = event.customProp;
		}
		
		public function validaFinal():void{
			if (info.guarda == true)
				guardaEntidad();
			else
				Alert.show("Debe capturar los datos requeridos",titulo,4,null,null,global.iAlert);
		}
		
		]]>
	</mx:Script>
	<mx:Canvas x="10" y="10" width="380" height="160" styleName="canvasMod">
		<mx:Label id="lblCodigo" x="36.75" y="12" text="Código:"/>
		<mx:Label id="lblDescripcion" x="12.75" y="44" text="Descripción:"/>
		<mx:Canvas x="10" y="101" width="358" height="40" styleName="canvasMod">
			<Data:CustomComboBox id="cboCritRiesgo" labelField="descripcion" x="10" y="7" width="336"/>
		</mx:Canvas>
		<mx:Label id="lblCriterio" x="10" y="82" text="Criterio de Riesgo"/>
		<mx:TextInput id="txtCodigo" x="85.75" y="10" width="50" maxChars="3"/>
		<mx:TextInput id="txtDescripcion" x="85.75" y="42" width="282.25"/>
		</mx:Canvas>
	<mx:Button id="btnAceptar" x="302" y="178" icon="@Embed(source='assets/images/iconAccept.png')" click="enviar()" width="40"/>
	<mx:Button id="btnCancelar" x="350" y="178" icon="@Embed(source='assets/images/iconCancel.png')" click="cerrar()" width="40"/>
</mx:TitleWindow>