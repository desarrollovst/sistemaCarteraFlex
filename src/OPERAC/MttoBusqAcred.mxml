<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" title="Nuevo" showCloseButton="true" 
	close="cerrar()" layout="absolute" width="810" height="340" creationPolicy="all">
	
	<mx:Script>
		<![CDATA[
			import Data.Globales;
			import Data.Utils;
			import mx.collections.ArrayCollection;
			import mx.collections.IList;
			import mx.controls.Alert;
			import mx.core.Application;
			import mx.core.DragSource;
			import mx.events.*;
            import mx.managers.DragManager;
            import mx.managers.PopUpManager;
            import mx.rpc.events.ResultEvent;
			import mx.rpc.Fault;
			import mx.rpc.soap.WebService;

			[Bindable]
			public var _xmlAcred:XML =  new XML();
			
			public var wsMS:WebService;
			public var indice:int;
			public var grupo:String;
			private var du:Utils;	
			private var global:Globales;
			private var dropInitiator:String
			
			public var acredObj:ArrayCollection = new ArrayCollection();
			
			public var comMttoFecha:MttoSelFecha;
						
			public function agregaAcred(event:MouseEvent):void{
				var fecha:String = comMttoFecha.dtfFecha.text;
				PopUpManager.removePopUp(comMttoFecha);
				var dataInfo:ArrayCollection = destGrid.dataProvider as ArrayCollection
				dataInfo[indice].FECHA = fecha; 
				destGrid.dataProvider = dataInfo;
			}			
						
			private function buscarAcred():void{
				var wsCat:WebService = new WebService();
				var params:Array = new Array();
				if (txtCodAcred.text != "" || txtAcred.text != ""){
					btnBuscarAcred.setFocus();
					
					du.initWsCat(wsCat);
					du.sCursor();
					Application.application.bloquear();
					
					du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
						_xmlAcred = new XML(evt.result);
								
						du.rCursor();
						du.closeWs(wsCat);
						Application.application.desbloquear();								
					});
					params[0] = "";
					params[1] = txtCodAcred.text;
					params[2] = txtAcred.text;
					wsCat.getInfoAcred.send(3, params);
				}
			}
			
			public function cerrar():void{
				PopUpManager.removePopUp(this);
			}
			
			public function eliminarAcred():void{
				if (destGrid.selectedIndex >= 0){
					var ind:int = destGrid.selectedIndex;
					var dataInfo:ArrayCollection = destGrid.dataProvider as ArrayCollection;
					dataInfo.removeItemAt(ind);
					destGrid.dataProvider = dataInfo;
				}
				else
					Alert.show("Debe seleccionar un registro de Acreditado", "Alta Acreditados", 4,null,null,global.iAlert);		
			}
			
			public function init(grupo:String):void{
				global = new Globales();
				du = new Utils();
				destGrid.dataProvider = new ArrayCollection([]);
				this.grupo = grupo; 
			}
			
			public function initConexion():void{				
				wsMS = new WebService();			
				wsMS.wsdl = Application.application.wsStr.wsdlServ.toString();
				wsMS.loadWSDL();
			}	
			
			public function manejadorCerrar(e:CloseEvent):void{
				if(e.detail == Alert.OK)
					PopUpManager.removePopUp(this);
			}
			
			private function validar():void{
				 var dataInfo:ArrayCollection = destGrid.dataProvider as ArrayCollection;
				 var registro:Array = new Array();
				 
				 if (dataInfo.length > 0){
				 	for(var i:int = 0; i < dataInfo.length; i++)
				 		registro[i] = grupo + "," + dataInfo[i].CODIGO + "," + dataInfo[i].FECHA; 
				 	
					initConexion();
					du.sCursor();
					Application.application.bloquear();
					
					du.ejecutaWsMethod(wsMS,function(evt:ResultEvent):void {											
					var res:String = new String(evt.result.toString());
						if(res.substr(0,1) == "1"){
							Alert.show("Cliente(s) incorporado(s) exitosamente.", "Alta Acreditados", Alert.OK,null,manejadorCerrar,global.iInfo);
						}	
						else
							Alert.show(res.substr(2,res.length-1), "Alta Acreditados", 4,null,null,global.iAlert);
							
						du.rCursor();
						du.closeWs(wsMS);
						Application.application.desbloquear();	
					});
					wsMS.setAgregaAcred.send(registro, Application.application.U_ID);			 		
				 }
				 else if (dataInfo.length == 0)
					Alert.show("Debe seleccionar un registro de Acreditado", "Alta Acreditados", 4,null,null,global.iAlert);
			}
			
			private function setDragInitator(event:MouseEvent, id:String):void{
                dropInitiator = id;
            }
            
            private function doDragEnter(event:DragEvent):void{
            	var dropTarget:DataGrid = DataGrid(event.currentTarget);
                var dataInfo:ArrayCollection = dropTarget.dataProvider as ArrayCollection;
                var items:Array = event.dragSource.dataForFormat("items") as Array;
                   
                var drop:Boolean = true;
                
                if(dropInitiator == dropTarget.id){
                	DragManager.showFeedback(DragManager.MOVE);
                    DragManager.acceptDragDrop(dropTarget);
                } 
                else {
                	if (dataInfo != null){
                    	for(var i:int = 0; i < dataInfo.length; i++)
                        	if(dataInfo[i].CODIGO == items[0].CODIGO) 
                        		drop = false;
                 	}
                 	else
                 		drop = false;
					if(drop){
                        DragManager.showFeedback(DragManager.MOVE);
                        DragManager.acceptDragDrop(dropTarget);
                    }
                }
            }

            private function doDragDrop(event:DragEvent):void{        
	            var dropTarget:DataGrid = DataGrid(event.currentTarget);
	           	doDragExit(event);  
	            var items:Array = event.dragSource.dataForFormat("items") as Array;
	            var dropLoc:int = dropTarget.calculateDropIndex(event); 
					
				indice = dropLoc;
					
	            for(var i:int = 0; i < items.length; i++){                 
	            	IList(dropTarget.dataProvider).addItemAt(items[i], dropLoc);
	            }
            }
            
            private function doDragExit(event:DragEvent):void{
            	var dropTarget:DataGrid = DataGrid(event.currentTarget);   
                var fec:Date;
                dropTarget.hideDropFeedback(event);
            	comMttoFecha = new MttoSelFecha();
            	comMttoFecha = MttoSelFecha(PopUpManager.createPopUp(this,MttoSelFecha,true));
				comMttoFecha.title = "Fecha de Incorporación";
				comMttoFecha.lblFecha.text = "Fecha de Incorporación del Cliente"; 
				fec = Application.application._Current_Fecha;
				comMttoFecha.fechaMin = fec;
				comMttoFecha.fechaMax = fec; 
				comMttoFecha.edita = false;
				//PARAMETRO IGUAL A 2 NO PERMITE OMITIR EL DATO
				comMttoFecha.init(2);
				comMttoFecha.btnAceptar.addEventListener(MouseEvent.CLICK, agregaAcred);
				PopUpManager.centerPopUp(comMttoFecha);
            }
			
		]]>
	</mx:Script>
	<mx:Label x="15" y="12" text="Código:" width="44"/>
	<mx:Label x="131.5" y="12" text="Nombre:" width="54"/>
	<mx:TextInput id="txtCodAcred" x="62.5" y="10" width="60" maxChars="6" restrict="0-9" enter="buscarAcred()"/>
	<mx:TextInput id="txtAcred" x="184.5" y="10" width="200.5" enter="buscarAcred()"/>
	<mx:Button x="760" y="268" icon="@Embed(source='assets/images/iconCancel.png')" click="cerrar()" id="btnCerrar" width="40"/>
	<mx:Button id="btnAceptar" x="712" y="268" icon="@Embed(source='assets/images/iconAccept.png')" click="validar()" width="40"/>
	<mx:DataGrid id="dgAcred" x="15" y="51" width="370" height="209" dataProvider="{_xmlAcred.Table}"
		allowMultipleSelection="false" dragMoveEnabled="true" mouseDown="setDragInitator(event, &apos;dropList&apos;)" 
		dragEnabled="true">
		<mx:columns>
			<mx:DataGridColumn headerText="Código" dataField="CODIGO" width="70"/>
			<mx:DataGridColumn headerText="Nombre" dataField="NOMBRE_CL"/>
		</mx:columns>
	</mx:DataGrid>
	<mx:Button id="btnBuscarAcred" x="393" y="11" icon="@Embed(source='assets/images/iconBusq.png')" click="buscarAcred()" toolTip="Buscar Acreditado" width="40"/>
	<mx:DataGrid id="destGrid" x="393" y="51" height="209" width="407" allowMultipleSelection="true"  
	    dragMoveEnabled="true" mouseDown="setDragInitator(event, &apos;destGrid&apos;)"
	    dragEnter="doDragEnter(event)" dragDrop="doDragDrop(event)" dragEnabled="false">
		<mx:columns>
			<mx:DataGridColumn headerText="Código" dataField="CODIGO" width="70"/>
			<mx:DataGridColumn headerText="Nombre" dataField="NOMBRE_CL"/>
			<mx:DataGridColumn headerText="Incorporación" dataField="FECHA" width="100"/>
		</mx:columns>
	</mx:DataGrid>
	<mx:Button id="btnEliminar" x="760" y="11" icon="@Embed(source='assets/images/iconDelete.png')" click="eliminarAcred()" toolTip="Eliminar Acreditado" width="40"/>
</mx:TitleWindow>