<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="400" height="390" creationPolicy="all" xmlns:Controls="Controls.*">
	<mx:Metadata>
		[Event(name="enviarDatosProsp", type="Data.EventProsp")]
	</mx:Metadata>
	
	<mx:Validator id="calleV" source="{txtCalle}" property="text" 
	invalid="{txtCalle.setFocus()}" triggerEvent="" requiredFieldError="Nombre de Calle Requerido"/>
	
	<!--<mx:Validator id="NoExtv" source="{txtNoExt}" property="text" 
	invalid="{txtNoExt.setFocus()}" triggerEvent="" requiredFieldError="Número Exterior Requerido"/>-->
	
	<mx:Validator id="codPostalV" source="{txtCodPostal}" property="text" 
	invalid="{txtCodPostal.setFocus()}" triggerEvent="" requiredFieldError="Código Postal Requerido"/>
	
	<mx:Validator id="entFedV" source="{comEntGrid.txtDato}" property="text" 
	invalid="{comEntGrid.txtDato.setFocus()}" triggerEvent="" requiredFieldError="Nombre de Entidad Federativa Requerido"/>
	
	<mx:Validator id="municipioV" source="{comMunGrid.txtDato}" property="text" 
	invalid="{comMunGrid.txtDato.setFocus()}" triggerEvent="" requiredFieldError="Nombre de Municipio Requerido"/>
	
	<mx:Validator id="localidadV" source="{comLocGrid.txtDato}" property="text" 
	invalid="{comLocGrid.txtDato.setFocus()}" triggerEvent="" requiredFieldError="Nombre de Localidad Requerido"/>
	
	<mx:Validator id="coloniaV" source="{comColGrid.txtDato}" property="text" 
	invalid="{comColGrid.txtDato.setFocus()}" triggerEvent="" requiredFieldError="Nombre de Colonia Requerido"/>

	<mx:Script>
		<![CDATA[
			import Data.DatosProsp;
			import Data.EventProsp;
			import Data.Globales;
			import Data.Utils;
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.core.Application;
			import mx.effects.easing.Elastic;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.soap.WebService;
			import mx.utils.StringUtil;
			import mx.validators.Validator;
			
			private var _xmlDir:XML =  new XML();
			private var _xmlColonia:XML =  new XML();
			private var _xmlEntidad:XML =  new XML();
			private var _xmlMunicipio:XML =  new XML();
			private var _xmlLocalidad:XML =  new XML();
			
			private var _xmlFiltro:XMLList;
						
			public var sucursal:Array = new Array();
			
			public var datos:DatosProsp = new DatosProsp();
			
			private var du:Utils;
			private var global:Globales;
			public var texto:String;
			
			//Realiza la busqueda de direccion del acreditado utilizando el codigo postal
			public function buscaCodPostal():void{ 
				var wsCat:WebService = new WebService();
				var params:Array = new Array();
				txtCodPostal.text = StringUtil.trim(txtCodPostal.text);
				if (txtCodPostal.text != "" && txtCodPostal.length == 5){
					du.initWsCat(wsCat);
					du.sCursor();
					
					_xmlDir = null;
				
					du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
						_xmlDir = new XML(evt.result.toString());
					
						limpiaDatosDir();
						
						du.rCursor();
						du.closeWs(wsCat);
						wsCat = null;
						
						if(_xmlDir.elements().length() > 0){
							comEntGrid.asignarTexto(_xmlDir.Table[0].ENTFED);
							comMunGrid.asignarTexto(_xmlDir.Table[0].MUNIC);
							comLocGrid.asignarTexto(_xmlDir.Table[0].LOC);
							comColGrid.asignarTexto(_xmlDir.Table[0].COL);
						
							datos.cdgDirEntFed = _xmlDir.Table[0].CDGEF;		
							datos.cdgMun = _xmlDir.Table[0].CDGMU;
							datos.cdgLoc = _xmlDir.Table[0].CDGLO;
							datos.cdgCol = _xmlDir.Table[0].CDGCOL;	
							
							obtieneMunicipio();
							obtieneLocalidad();
							obtieneColonia();
						}
					});
					params[0] = txtCodPostal.text;
					//obtiene la ubicacion de un domicilio con la informacion del codigo postal
					wsCat.getCatalogoGral.send(6, params);
				}
				else{
					txtCodPostal.text = "";
					obtieneMunicipio();
					obtieneLocalidad();
					obtieneColonia();
				}
			}
			
			public function enviarDatosProsp(formData:DatosProsp):void{
				var invalidArray:Array = Validator.validateAll([calleV, entFedV, municipioV, localidadV, coloniaV]);
				if(invalidArray.length == 0){	
					formData.calle = StringUtil.trim(txtCalle.text);
					formData.entreCalles = StringUtil.trim(txtEntreCalles.text);
					formData.noExt = "";//StringUtil.trim(txtNoExt.text);
					formData.noInt = "";//StringUtil.trim(txtNoInt.text);
					formData.telefono = StringUtil.trim(txtTelefono.text);
					formData.codPostal = txtCodPostal.text;
					formData.cdgDirEntFed = datos.cdgDirEntFed;
					formData.cdgMun = datos.cdgMun;
					formData.cdgLoc = datos.cdgLoc;
					formData.cdgCol = datos.cdgCol;
					formData.cdgPais = "MEX";
					formData.guardaDir = true;
				}
				else{
					formData.guardaDir = false;
				}
				var event:EventProsp = new EventProsp("enviarDatosProsp", formData);
				dispatchEvent(event);
			}
			
			public function init(info:DatosProsp):void{
				var wsCat:WebService = new WebService();
				global = new Globales();
				du = new Utils();
				
				du.initWsCat(wsCat);
				du.sCursor();
					
				_xmlEntidad = null;
				
				du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
					_xmlEntidad = new XML(evt.result.toString());
					
					if(_xmlEntidad.elements().length() > 0){
						comEntGrid.init(_xmlEntidad, 130);
						comEntGrid.addEventListener("seleccionar",selecEntFed);
						comEntGrid.addEventListener("verificar",verifEntFed);
					}
					
					du.rCursor();
					du.closeWs(wsCat);
					wsCat = null;
					
					if (info != null){
						txtCalle.text = info.calle;
						txtEntreCalles.text = info.entreCalles;
						//txtNoExt.text = info.noExt;
						//txtNoInt.text = info.noInt;
						txtTelefono.text = info.telefono;
						txtCodPostal.text = info.codPostal;
						comEntGrid.asignarTexto(info.dirEntFed);
						comMunGrid.asignarTexto(info.municipio);
						comLocGrid.asignarTexto(info.localidad);
						comColGrid.asignarTexto(info.colonia);
						
						datos.cdgDirEntFed = info.cdgDirEntFed;		
						datos.cdgMun = info.cdgMun;
						datos.cdgLoc = info.cdgLoc;
						datos.cdgCol = info.cdgCol;	
						
						obtieneMunicipio();
						obtieneLocalidad();
						obtieneColonia();
					}	
				});
				//Obtiene informacion de la entidad federativa
				wsCat.getCatalogoGral.send(2);
			}
			
			public function limpiaDatosDir():void{
				comEntGrid.limpiar();
				comMunGrid.limpiar();
				comLocGrid.limpiar();
				comColGrid.limpiar();
						
				datos.cdgDirEntFed = "";		
				datos.cdgMun = "";
				datos.cdgLoc = "";
				datos.cdgCol = "";	
			}
			
			public function obtieneColonia():void{
				var wsCat:WebService = new WebService();
				var params:Array = new Array();
				
				du.initWsCat(wsCat);
				du.sCursor();
					
				du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
					_xmlColonia = new XML(evt.result.toString());
					
					comColGrid.init(_xmlColonia, 90);
					if(_xmlColonia.elements().length() > 0){
						comColGrid.addEventListener("seleccionar",selecColonia);
					}
					
					du.rCursor();
					du.closeWs(wsCat);
					wsCat = null;	
				});
				//obtiene informacion de las colonias coincidentes
				params[0] = datos.cdgDirEntFed;
				params[1] = datos.cdgMun;
				params[2] = datos.cdgLoc;
				params[3] = txtCodPostal.text;
				wsCat.getCatalogoGral.send(5, params);
			}
			
			public function obtieneLocalidad():void{
				var wsCat:WebService = new WebService();
				var params:Array = new Array();
				
				du.initWsCat(wsCat);
				du.sCursor();
					
				du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
					_xmlLocalidad = new XML(evt.result.toString());
					
					comLocGrid.init(_xmlLocalidad, 90);
					if(_xmlLocalidad.elements().length() > 0){
						comLocGrid.addEventListener("seleccionar",selecLocalidad);
						comLocGrid.addEventListener("verificar",verifLocalidad);
					}
					
					du.rCursor();
					du.closeWs(wsCat);
					wsCat = null;
				});
				//Obtiene la informacion de las localidades coincidentes
				params[0] = datos.cdgDirEntFed;
				params[1] = datos.cdgMun;
				params[2] = txtCodPostal.text;
				wsCat.getCatalogoGral.send(4, params);
			}
			
			public function obtieneMunicipio():void{
				var wsCat:WebService = new WebService();
				var params:Array = new Array();
				
				du.initWsCat(wsCat);
				du.sCursor();
					
				du.ejecutaWsMethod(wsCat,function(evt:ResultEvent):void {											
					_xmlMunicipio = new XML(evt.result.toString());
					
					comMunGrid.init(_xmlMunicipio, 130);
					if(_xmlMunicipio.elements().length() > 0){
						comMunGrid.addEventListener("seleccionar",selecMunic);
						comMunGrid.addEventListener("verificar",verifMunic);
					}
					
					du.rCursor();
					du.closeWs(wsCat);
					wsCat = null;	
				});
				params[0] = datos.cdgDirEntFed;
				params[1] = txtCodPostal.text;
				//obtiene informacion del municipio
				wsCat.getCatalogoGral.send(3, params);
			}
			
			private function selecColonia(event:Event):void{
				datos.cdgCol = event.currentTarget.obtenerCodigo();
				_xmlFiltro = _xmlColonia.Table.(CODIGO.toString() == datos.cdgCol)
				if(txtCodPostal.text != _xmlFiltro.CDGPOSTAL.toString())
					txtCodPostal.text = _xmlFiltro.CDGPOSTAL.toString();
				comColGrid.setFocus();
			}
			
			private function selecEntFed(event:Event):void{
				if(datos.cdgDirEntFed != event.currentTarget.obtenerCodigo()){
					comMunGrid.limpiar();
					comLocGrid.limpiar();
					comColGrid.limpiar();
					txtCodPostal.text = "";
					datos.cdgDirEntFed = event.currentTarget.obtenerCodigo();
					datos.cdgMun = "";
					datos.cdgLoc = "";
					datos.cdgCol = "";
					obtieneMunicipio();
				}
				comEntGrid.setFocus();
			}
			
			private function selecLocalidad(event:Event):void{
				if(datos.cdgLoc != event.currentTarget.obtenerCodigo()){
					comColGrid.limpiar();
					txtCodPostal.text = "";
					datos.cdgLoc = event.currentTarget.obtenerCodigo();
					datos.cdgCol = "";
					obtieneColonia();
				}
				comLocGrid.setFocus();
			}
			
			private function selecMunic(event:Event):void{
				if(datos.cdgMun != event.currentTarget.obtenerCodigo()){	
					comLocGrid.limpiar();
					comColGrid.limpiar();
					txtCodPostal.text = "";
					datos.cdgMun = event.currentTarget.obtenerCodigo();
					datos.cdgLoc = "";
					datos.cdgCol = "";
					obtieneLocalidad();
				}
				comMunGrid.setFocus();
			}		
			
			private function verifEntFed(event:Event):void{
				if(comEntGrid.obtenerCodigo() == ""){
					comMunGrid.limpiar();
					comLocGrid.limpiar();
					comColGrid.limpiar();
					datos.cdgMun = "";
					datos.cdgLoc = "";
					datos.cdgCol = ""; 
				}
			}
			
			private function verifLocalidad(event:Event):void{
				if(comColGrid.obtenerCodigo() == ""){
					comColGrid.limpiar();
					datos.cdgCol = ""; 
				}
			}
			
			private function verifMunic(event:Event):void{
				if(comMunGrid.obtenerCodigo() == ""){
					comLocGrid.limpiar();
					comColGrid.limpiar();
					datos.cdgLoc = "";
					datos.cdgCol = ""; 
				}
			}	
	]]>
	</mx:Script>
	<!--<mx:FormItem label="Número Exterior:" id="NoExt" visible="true" x="57.5" y="80" width="249">
		<mx:TextInput id="txtNoExt" maxChars="20" width="90"/>
	</mx:FormItem>
	<mx:FormItem label="Número Interior:" id="NoInt" visible="true" x="61.5" y="112" width="249">
		<mx:TextInput id="txtNoInt" maxChars="20" width="90"/>
	</mx:FormItem>-->
	<mx:Label id="lblCalle" text="Calle:" x="117.5" y="19"/>
	<mx:TextInput id="txtCalle" maxChars="80" restrict="A-Z;a-z;Ñ;ñ;0-9; " width="170" x="156.5" y="18"/>
	<mx:Label id="lblEntre" text="Entre las calles:" x="67.5" y="49"/>
	<mx:TextInput id="txtEntreCalles" maxChars="50" restrict="A-Z;a-z;Ñ;ñ;0-9; " width="170" x="156.5" y="48"/>
	<mx:Label id="lblTelefono" text="Teléfono:" x="98.5" y="79"/>
	<mx:TextInput id="txtTelefono" restrict="0-9" maxChars="20" x="156.5" y="78"/>
	<mx:Label id="lblCodPostal" text="Código Postal:" x="75.5" y="109"/>
	<mx:TextInput id="txtCodPostal" enter="buscaCodPostal()" width="95" maxChars="5" restrict="0-9" x="156.5" y="108"/>
	<mx:Label id="lblEntFed" text="Entidad Federativa:" x="51.5" y="140"/>
	<Controls:TextGrid id="comEntGrid" height="24" x="156.5" y="138"/>
	<mx:Label id="lblMunicipio" text="Municipio:" x="98.5" y="171"/>
	<Controls:TextGrid id="comMunGrid" height="24" x="156.5" y="170"/>
	<mx:Label id="lblLocalidad" text="Localidad:" x="94.5" y="204"/>
	<Controls:TextGrid id="comLocGrid" height="24" x="156.5" y="202"/>
	<mx:Label id="lblColonia" text="Colonia:" x="105.5" y="237"/>
	<Controls:TextGrid id="comColGrid" height="24" x="156.5" y="234"/>
</mx:Canvas>